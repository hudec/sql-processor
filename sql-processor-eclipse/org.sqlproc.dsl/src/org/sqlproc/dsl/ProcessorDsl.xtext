grammar org.sqlproc.dsl.ProcessorDsl hidden(ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

generate processorDsl "http://www.sqlproc.org/dsl/ProcessorDsl"

Artifacts: {Artifacts}
    WS* (properties+=Property WS*|pojos+=PojoDefinition WS*|tables+=TableDefinition WS*
    	|procedures+=ProcedureDefinition WS*|functions+=FunctionDefinition WS*
    	|statements+=MetaStatement WS*|mappings+=MappingRule WS*|features+=OptionalFeature WS*
    	|pojoPackages+=PackageDeclaration WS*
    )*;

DatabaseCatalogAssignement: dbCatalog=PropertyValue;
DatabaseSchemaAssignement: dbSchema=PropertyValue;
DatabaseTypeAssignement: dbType=IDENT;
DatabaseMetaInfoAssignement: dbMetaInfo=PropertyValue;
DriverMetaInfoAssignement: dbDriverInfo=PropertyValue;
DriverMethodOutputAssignement: driverMethod=IDENT '->' callOutput=PropertyValue;
SqlTypeAssignement: typeName=IDENT (LPAREN size=NUMBER RPAREN)? '->' type=PojoType;
ColumnTypeAssignement: dbColumn=IDENT '->' type=PojoType;
ShowColumnTypeAssignement: dbColumn=IDENT '->' type=PropertyValue;
TableAssignement: dbTable=IDENT '->' newName=IDENT;
JoinTableAssignement: dbTable=IDENT ('->' dbTables+=IDENT)+;
ColumnAssignement: dbColumn=IDENT '->' newName=IDENT;
ImportAssignement: dbColumn=IDENT '->' pkTable=IDENT ('->' pkColumn=IDENT)?;
ExportAssignement: dbColumn=IDENT '->' fkTable=IDENT ('->' fkColumn=IDENT)?;
InheritanceAssignement: discriminator=IDENT '->' dbTable=IDENT ('->' dbColumns+=IDENT)+;
ManyToManyAssignement: pkColumn=IDENT '->' pkTable=IDENT ('->' fkColumn=IDENT)?;
DebugLevelAssignement: debug=IDENT;

Property:
	(name='resolve-pojo-on'
	|name='resolve-pojo-off'
	|name='database-' database=DatabaseProperty
	|name='pojogen-' pojogen=PojogenProperty
	|name='metagen-' metagen=MetagenProperty
	|name='daogen-' daogen=DaogenProperty
	) SEMICOLON;

DatabaseProperty:
	(name='is-online'
	|name='is-offline'
	|name='has-url' WS+ dbUrl=PropertyValue
	|name='login-username' WS+ dbUsername=PropertyValue
	|name='login-password' WS+ dbPassword=PropertyValue
	|name='in-catalog' WS+ dbCatalog=DatabaseCatalogAssignement
	|name='active-schema' WS+ dbSchema=DatabaseSchemaAssignement
	|name='jdbc-driver' WS+ dbDriver=PropertyValue
	|name='ddl-create' WS+ dbExecuteBefore=PropertyValue
	|name='ddl-drop' WS+ dbExecuteAfter=PropertyValue
	|name='index-types' WS+ dbIndexTypes=PropertyValue
	|name='skip-indexes'
	|name='skip-functions-procedures'
	|name='is-of-type' WS+ dbType=DatabaseTypeAssignement
	|name='show-database-info' WS+ dbMetaInfo=DatabaseMetaInfoAssignement
	|name='show-driver-info' WS+ dbDriverInfo=DriverMetaInfoAssignement
	|name='show-driver-output' (WS+ dbMethodsCalls+=DriverMethodOutputAssignement)+
	|name='debug-level' WS+ debug=DebugLevelAssignement
	);

PojogenProperty:
	(name='types-sqltypes' (WS+ sqlTypes+=SqlTypeAssignement)+
	|name='types-in-table' WS+ dbTable=IDENT (WS+ sqlTypes+=SqlTypeAssignement)+
	|name='types-for-columns' WS+ dbTable=IDENT (WS+ columnTypes+=ColumnTypeAssignement)+
	|name='show-type-for-column' WS+ dbTable=IDENT WS+ columnType=ShowColumnTypeAssignement
	|name='ignore-tables' (WS+ dbTables+=IDENT)+
	|name='only-tables' (WS+ dbTables+=IDENT)+
	|name='not-abstract-tables' (WS+ dbTables+=IDENT)+
	|name='join-tables' (WS+ joinTables+=JoinTableAssignement)+
	|name='ignore-columns' WS+ dbTable=IDENT (WS+ dbColumns+=IDENT)+
	|name='required-columns' WS+ dbTable=IDENT (WS+ dbColumns+=IDENT)+
	|name='not-required-columns' WS+ dbTable=IDENT (WS+ dbColumns+=IDENT)+
	|name='create-columns' WS+ dbTable=IDENT (WS+ columnTypes+=ColumnTypeAssignement)+
	|name='rename-tables' (WS+ tables+=TableAssignement)+
	|name='rename-columns' WS+ dbTable=IDENT (WS+ columns+=ColumnAssignement)+
	|name='ignore-one-to-many' WS+ dbTable=IDENT (WS+ exports+=ExportAssignement)+ // exports
	|name='ignore-many-to-one' WS+ dbTable=IDENT (WS+ imports+=ImportAssignement)+ // imports
	|name='inherit-many-to-one' WS+ dbTable=IDENT (WS+ imports+=ImportAssignement)+ // imports
	|name='create-one-to-many' WS+ dbTable=IDENT (WS+ exports+=ExportAssignement)+ // exports
	|name='create-many-to-one' WS+ dbTable=IDENT (WS+ imports+=ImportAssignement)+ // imports
	|name='table-many-to-many' WS+ dbTable=IDENT (WS+ many2s+=ManyToManyAssignement)+
	|name='inherit-discriminator' WS+ dbTable=IDENT WS+ dbColumn=IDENT (WS+ inheritance+=InheritanceAssignement)+
	|name='generate-methods' (WS+ methods+=IDENT)+
	|name='implements-interfaces' (WS+ toImplements+=[jvmTypes::JvmType|QualifiedName])+
	|name='extends-class' WS+ toExtends=[jvmTypes::JvmType|QualifiedName]
	|name='generate-wrappers'
	|name='implementation-package' WS+ implPackage=IDENT
	|name='make-it-final'
	|name='version-column' WS+ version=IDENT (WS+ dbTables+=IDENT)*
	|name='debug-level' WS+ debug=DebugLevelAssignement
	);

MetaTypeAssignement: dbColumn=IDENT '->' type=IDENT ('->' extension=IDENT)?;

MetagenProperty:
	(name='global-sequence' WS+ sequence=IDENT (WS+ type=IDENT)?
	|name='table-sequence' WS+ dbTable=IDENT WS+ sequence=IDENT (WS+ type=IDENT)?
	|name='global-identity' (WS+ identity=IDENT (WS+ type=IDENT)?)?
	|name='table-identity' WS+ dbTable=IDENT WS+ identity=IDENT (WS+ type=IDENT)?
	|name='column-meta-type' WS+ dbTable=IDENT (WS+ metaTypes+=MetaTypeAssignement)+
	|name='statement-meta-type' WS+ dbStatement=IDENT (WS+ metaTypes+=MetaTypeAssignement)+
	|name='make-it-final'
	|name='like-columns' WS+ dbTable=IDENT (WS+ dbColumns+=IDENT)+
	|name='not-like-columns' WS+ dbTable=IDENT (WS+ dbColumns+=IDENT)+
	|name='generate-sequences'
	|name='generate-identities'
	|name='function-result' WS+ dbFunction=IDENT WS+ type=IDENT
	|name='function-result-set' WS+ dbFunction=IDENT WS+ dbTable=IDENT
	|name='procedure-result-set' WS+ dbProcedure=IDENT WS+ dbTable=IDENT
	|name='debug-level' WS+ debug=DebugLevelAssignement
	);

DaogenProperty:
	(name='ignore-tables' (WS+ dbTables+=IDENT)+
	|name='only-tables' (WS+ dbTables+=IDENT)*
	|name='implementation-package' WS+ implPackage=IDENT
	|name='implements-interfaces' (WS+ toImplements+=[jvmTypes::JvmType|QualifiedName])+
	|name='extends-class' WS+ toExtends=[jvmTypes::JvmType|QualifiedName]
	|name='make-it-final'
	|name='function-result' WS+ dbFunction=IDENT WS+ resultType=PojoType
	|name='debug-level' WS+ debug=DebugLevelAssignement
	);

PropertyValue:
  (REST|IDENT_DOT|IDENT|NUMBER|COLON|STRING|COMMA|MINUS|PLUS|LPAREN|RPAREN|LBRACE|RBRACE|QUESTI|NOT|BAND|BOR|HASH|AT|CARET|EQUALS|LESS_THAN|MORE_THAN|PERCENT|DOT|AND|OR|ESC_CHAR|ON_OFF|STATEMENT_TYPE|MAPPING_TYPE|OPTION_TYPE)
  (=> (REST|IDENT_DOT|IDENT|NUMBER|WS|COLON|STRING|COMMA|MINUS|PLUS|LPAREN|RPAREN|LBRACE|RBRACE|QUESTI|NOT|BAND|BOR|HASH|AT|CARET|EQUALS|LESS_THAN|MORE_THAN|PERCENT|DOT|AND|OR|ESC_CHAR|ON_OFF|STATEMENT_TYPE|MAPPING_TYPE|OPTION_TYPE)*);

PojoDefinition hidden(ML_COMMENT, SL_COMMENT, WS):
	'pojo' name=IDENT class=(IDENT|IDENT_DOT) SEMICOLON;

TableDefinition hidden(ML_COMMENT, SL_COMMENT, WS):
	'table' name=IDENT table=IDENT SEMICOLON;

ProcedureDefinition hidden(ML_COMMENT, SL_COMMENT, WS):
	'procedure' name=IDENT table=IDENT SEMICOLON;

FunctionDefinition hidden(ML_COMMENT, SL_COMMENT, WS):
	'function' name=IDENT table=IDENT SEMICOLON;

MetaStatement:
	name=IDENT LPAREN type=STATEMENT_TYPE (COMMA modifiers+=StatementModifier)* RPAREN EQUALS statement=Sql SEMICOLON;

Sql: (sqls+=SqlFragment)+;

SqlFragment:
	value=SqlValue
    | AT col=Column
    | STRING cnst=Constant
    | COLON ident=Identifier
    | LBRACE meta=MetaSql RBRACE
    | PERCENT (PERCENT dbtab=DatabaseTable|dbcol=DatabaseColumn)
    ;

SqlValue: //~(COLON | STRING | AT | LBRACE | SEMICOLON)
  (REST|IDENT_DOT|IDENT|NUMBER|WS|COMMA|MINUS|PLUS|LPAREN|RPAREN|RBRACE|QUESTI|NOT|BAND|BOR|HASH|CARET|EQUALS|LESS_THAN|MORE_THAN|DOT|AND|OR|ESC_CHAR|ON_OFF|STATEMENT_TYPE|MAPPING_TYPE|OPTION_TYPE)
  (=> (REST|IDENT_DOT|IDENT|NUMBER|WS|COMMA|MINUS|PLUS|LPAREN|RPAREN|RBRACE|QUESTI|NOT|BAND|BOR|HASH|CARET|EQUALS|LESS_THAN|MORE_THAN|DOT|AND|OR|ESC_CHAR|ON_OFF|STATEMENT_TYPE|MAPPING_TYPE|OPTION_TYPE)*);


MetaSql:
    WS ifs+=IfSql (BOR ifs+=IfSql)*
    | type=QUESTI cond=IfSqlCond BOR ifs+=IfSql (BOR ifs+=IfSql)*
    | type=BAND ifs+=IfSql (BOR ifs+=IfSql)*
    | type=BOR ifs+=IfSql (BOR ifs+=IfSql)*
    | type=EQUALS WS* ftype=IDENT ifs+=IfSql
    | type=HASH NUMBER ord=OrdSql
    ;

IfSql: (sqls+=IfSqlFragment)+;

IfSqlFragment:
	value=IfSqlValue
    | AT col=Column
    | STRING cnst=Constant
    | COLON ident=Identifier
    | PERCENT (PERCENT dbtab=DatabaseTable|dbcol=DatabaseColumn)
    | LBRACE meta=IfMetaSql RBRACE
    ;

IfSqlValue: //~(COLON | STRING | AT | LBRACE | BOR | RBRACE)
  (REST|IDENT_DOT|IDENT|NUMBER|WS|SEMICOLON|COMMA|MINUS|PLUS|LPAREN|RPAREN|QUESTI|NOT|BAND|HASH|CARET|EQUALS|LESS_THAN|MORE_THAN|DOT|AND|OR|ESC_CHAR|ON_OFF|STATEMENT_TYPE|MAPPING_TYPE|OPTION_TYPE)
  (=> (REST|IDENT_DOT|IDENT|NUMBER|WS|SEMICOLON|COMMA|MINUS|PLUS|LPAREN|RPAREN|QUESTI|NOT|BAND|HASH|CARET|EQUALS|LESS_THAN|MORE_THAN|DOT|AND|OR|ESC_CHAR|ON_OFF|STATEMENT_TYPE|MAPPING_TYPE|OPTION_TYPE)*);

IfMetaSql:
    WS ifs+=IfSql (BOR ifs+=IfSql)*
    | type=QUESTI cond=IfSqlCond BOR ifs+=IfSql (BOR ifs+=IfSql)*
    | type=BAND ifs+=IfSql (BOR ifs+=IfSql)*
    | type=BOR ifs+=IfSql (BOR ifs+=IfSql)*
    ;

IfSqlCond:
    WS* bool1=IfSqlBool WS* (oper+=(AND|OR) WS* bool2+=IfSqlBool WS*)*;

IfSqlBool:
    (not?=NOT)? STRING cnst=Constant
    | (not?=NOT)? COLON ident=Identifier
    | (not?=NOT)? LPAREN cond=IfSqlCond RPAREN
    ;

OrdSql: (sqls+=OrdSql2)+;

OrdSql2:
    value=OrdSqlValue
    | STRING cnst=Constant
    | COLON ident=Identifier
    | PERCENT dbcol=DatabaseColumn
    ;

OrdSqlValue: //~(COLON | STRING | PERCENT | RBRACE)
  (REST|IDENT_DOT|IDENT|NUMBER|WS|SEMICOLON|COMMA|MINUS|PLUS|LPAREN|RPAREN|LBRACE|QUESTI|NOT|BAND|BOR|HASH|AT|CARET|EQUALS|LESS_THAN|MORE_THAN|DOT|AND|OR|ESC_CHAR|ON_OFF|STATEMENT_TYPE|MAPPING_TYPE|OPTION_TYPE)
  (=> (REST|IDENT_DOT|IDENT|NUMBER|WS|SEMICOLON|COMMA|MINUS|PLUS|LPAREN|RPAREN|LBRACE|QUESTI|NOT|BAND|BOR|HASH|AT|CARET|EQUALS|LESS_THAN|MORE_THAN|DOT|AND|OR|ESC_CHAR|ON_OFF|STATEMENT_TYPE|MAPPING_TYPE|OPTION_TYPE)*);

Column:
    columns += ExtendedColumn (=> columns += ExtendedColumn)*;

ExtendedColumn: 
	col=ExtendedColumnName (=> LPAREN modifiers+=Modifier (COMMA modifiers+=Modifier)* RPAREN)?;

ExtendedColumnName: 
	name=(IDENT|NUMBER|IDENT_DOT);

Constant:
    (case=(PLUS|MINUS))? name=(IDENT|IDENT_DOT)
    (=> LPAREN modifiers+=Modifier (COMMA modifiers+=Modifier)* RPAREN)?;

Identifier:
    (mode=(EQUALS|LESS_THAN|MORE_THAN))? (case=(PLUS|MINUS))? name=(IDENT|NUMBER|IDENT_DOT)
    (=> LPAREN modifiers+=Modifier (COMMA modifiers+=Modifier)* RPAREN)?;

DatabaseColumn:
    name=(IDENT|IDENT_DOT);

DatabaseTable:
    name=(IDENT|IDENT_DOT);

MappingRule:
	name=IDENT LPAREN type=MAPPING_TYPE (COMMA modifiers+=MappingRuleModifier)* RPAREN EQUALS mapping=Mapping SEMICOLON;

Mapping:
	WS* mappingItems+=MappingItem (WS+ mappingItems+=MappingItem)* WS*;

MappingItem:
	name=(IDENT|NUMBER) (STRING attr = MappingColumn)?;

MappingColumn:
	items += ExtendedMappingItem (=> items += ExtendedMappingItem)*;
	
ExtendedMappingItem:
	attr=MappingColumnName (=> LPAREN modifiers+=MappingItemModifier (COMMA modifiers+=MappingItemModifier)* RPAREN)?;

MappingColumnName:
    name=(IDENT|IDENT_DOT|NUMBER);

OptionalFeature:
	name=IDENT LPAREN type=OPTION_TYPE (COMMA modifiers+=OptionalFeatureModifier)* RPAREN EQUALS option=FeatureValue SEMICOLON;

StatementModifier: (IDENT | NUMBER);
MappingRuleModifier: (IDENT | NUMBER);
MappingItemModifier: (IDENT | NUMBER);
OptionalFeatureModifier: (IDENT | NUMBER);
Modifier: (IDENT | NUMBER);

terminal ON_OFF: 'ON'|'OFF';
terminal STATEMENT_TYPE: 'QRY'|'CRUD'|'CALL';
terminal MAPPING_TYPE: 'OUT';
terminal OPTION_TYPE: 'OPT'|'LOPT'|'IOPT'|'SOPT'|'BOPT';

//terminal IDENT_TYPE: IDENT LPAREN NUMBER (COMMA NUMBER)? RPAREN;
terminal IDENT_DOT: IDENT (DOT IDENT)+;
terminal IDENT: ('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'0'..'9'|'_'|'=')*;
terminal NUMBER: ('0'..'9')+;
//terminal NEGATIVE_NUMBER: '-'('0'..'9')+;

terminal ESC_CHAR:  '\\' (COLON | SEMICOLON | STRING | LBRACE | RBRACE | BOR | HASH | AT | PERCENT | DOT | '/');
terminal ML_COMMENT : '/*' -> '*/' ;
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')? ;
terminal WS:    (' '|'\t'|'\n'|'\r')+ ;
terminal AND:   '&' '&' ;
terminal OR:    '|' '|' ;

terminal COLON:    ':' ;
terminal SEMICOLON:';' ;
terminal STRING:   '$' ;
terminal COMMA:    ',' ;
terminal MINUS:    '-' ;
terminal PLUS:     '+' ;
terminal LPAREN:   '(' ;
terminal RPAREN:   ')' ;
terminal LBRACE:   '{' ;
terminal RBRACE:   '}' ;
terminal QUESTI:   '?' ;
terminal NOT:      '!' ;
terminal BAND:     '&' ;
terminal BOR:      '|' ;
terminal HASH:     '#' ;
terminal AT:	   '@';
terminal CARET:    '^';
terminal EQUALS:   '=' ;
terminal LESS_THAN:'<' ;
terminal MORE_THAN:'>' ;
terminal PERCENT:  '%' ;
terminal DOT:      '.' ;

terminal REST: !(SEMICOLON);


FeatureValue:
  (REST|IDENT_DOT|IDENT|NUMBER|WS|COLON|STRING|COMMA|MINUS|PLUS|LPAREN|RPAREN|LBRACE|RBRACE|QUESTI|NOT|BAND|BOR|HASH|AT|CARET|EQUALS|LESS_THAN|MORE_THAN|PERCENT|AND|OR|ESC_CHAR|ON_OFF|STATEMENT_TYPE|MAPPING_TYPE|OPTION_TYPE)+;

//SimpleType:
//	(native = ('_void'|'_char'|'_byte'|'_short'|'_int'|'_long'|'_float'|'_double'|'_boolean') |
//	(COLON (COLON ref=[PojoEntity|IDENT]) | type=[jvmTypes::JvmType|QualifiedName]));

PojoType:
	(native = ('_void'|'_char'|'_byte'|'_short'|'_int'|'_long'|'_float'|'_double'|'_boolean') | 
	(COLON (COLON ref=[PojoEntity|IDENT] | type=[jvmTypes::JvmType|QualifiedName]))
	(LESS_THAN (COLON gref=[PojoEntity|IDENT] | gtype=[jvmTypes::JvmType|QualifiedName]) MORE_THAN)?) (array ?= '[]')?;

PackageDeclaration hidden(ML_COMMENT, SL_COMMENT, WS):
	'package' name=(IDENT|IDENT_DOT) ('suffix' suffix=(IDENT|NUMBER))? LBRACE
		elements+=AbstractPojoEntity*
	RBRACE;

AbstractPojoEntity hidden(ML_COMMENT, SL_COMMENT, WS):
	PackageDeclaration | PojoEntity | Import | Implements | Extends | ImplPackage | PojoDao;

Import hidden(ML_COMMENT, SL_COMMENT, WS):
	'import' importedNamespace = QualifiedNameWithWildcard;

Implements hidden(ML_COMMENT, SL_COMMENT, WS):
	'implements' implements = [jvmTypes::JvmType|QualifiedName];

Extends hidden(ML_COMMENT, SL_COMMENT, WS):
	'extends' extends = [jvmTypes::JvmType|QualifiedName];

ImplPackage hidden(ML_COMMENT, SL_COMMENT, WS):
	'implementation-package' name=(IDENT|IDENT_DOT);

PojoEntityModifier1: ((final?='final') | (abstract?='abstract'));

PojoEntityModifier2: (('extends' superType=[PojoEntity|IDENT]) | ('discriminator' discriminator=(IDENT|NUMBER)) | ('serializable' sernum=NUMBER));

PojoEntity hidden(ML_COMMENT, SL_COMMENT, WS):
	(modifiers1+=PojoEntityModifier1)* 'pojo' name=IDENT (modifiers2+=PojoEntityModifier2)* 
	LBRACE
		features+=PojoProperty*
	RBRACE;

PojoPropertyModifier: ((required?='required') | (discriminator?='discriminator') | (primaryKey?='primaryKey') | ('index' index=NUMBER) | (version?='optLock'));

PojoProperty hidden(ML_COMMENT, SL_COMMENT, WS):
	name=IDENT (native = ('_char'|'_byte'|'_short'|'_int'|'_long'|'_float'|'_double'|'_boolean') | 
	(COLON (COLON (COLON (attrs+=[PojoProperty|IDENT])* | ref=[PojoEntity|IDENT]) | type=[jvmTypes::JvmType|QualifiedName]))
	(LESS_THAN (COLON gref=[PojoEntity|IDENT] | gtype=[jvmTypes::JvmType|QualifiedName]) MORE_THAN)?) (array ?= '[]')?
	(modifiers+=PojoPropertyModifier)*
;

PojoDaoModifier: (('extends' superType=[PojoDao|IDENT]) | ('serializable' sernum=NUMBER));

PojoDao hidden(ML_COMMENT, SL_COMMENT, WS):
	(modifiers1+=PojoEntityModifier1)* 'dao' name=IDENT (COLON COLON pojo=[PojoEntity|IDENT])?
	  (modifiers2+=PojoDaoModifier)* 
	LBRACE
		methods+=PojoMethod*
		toInits+=ToInitMethod*
	RBRACE;

PojoMethodModifier: ((callFunction?='callFunction') | (callUpdate?='callUpdate') | (callQuery?='callQuery') | (callQueryFunction?='callQueryFunction') | (callSelectFunction?='callSelectFunction'));

PojoMethod hidden(ML_COMMENT, SL_COMMENT, WS):
		(modifiers1+=PojoMethodModifier)* name=IDENT (type=PojoType COLON COLON COLON (args+=PojoMethodArg)+)?
;

ToInitMethod hidden(ML_COMMENT, SL_COMMENT, WS):
		name=IDENT COLON COLON COLON (args+=PojoMethodArg)+
;

PojoMethodArg hidden(ML_COMMENT, SL_COMMENT, WS):
	name=IDENT type=PojoType
;

QualifiedNameWithWildcard:
  QualifiedName '.*'?; 

QualifiedName: (IDENT|IDENT_DOT);

//     (REST|IDENT_DOT|IDENT|NUMBER|WS|COLON|SEMICOLON|STRING|COMMA|MINUS|PLUS|LPAREN|RPAREN|LBRACE|RBRACE|QUESTI|NOT|BAND|BOR|HASH|AT|CARET|EQUALS|LESS_THAN|MORE_THAN|PERCENT|AND|OR|ESC_CHAR)
